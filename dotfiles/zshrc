unsetopt autocd

# Path where dotfiles repository is installed
DESKTOP_PATH=$(dirname $(dirname $(realpath ${(%):-%x})))
# User's home directory, might be different from HOME if run as root
USER_HOME=$(echo $DESKTOP_PATH | grep -oE '^/[^/]+/[^/]+')

bindkey -e

# X terminal keys
bindkey '^[[1;5D' vi-backward-word
bindkey '^[[1;5C' vi-forward-word
bindkey '^[OF' vi-end-of-line
bindkey '^[OH' vi-beginning-of-line
bindkey '^[[3~' delete-char
bindkey '^[[3;5~' delete-word

# Non-X terminal keys, where different
# bindkey '^[[D' vi-backward-word  # FIXME: this is normal left arrow on macOS
# bindkey '^[[C' vi-forward-word  # FIXME: this is normal right arrow on macOS
bindkey '^[[4~' vi-end-of-line
bindkey '^[[1~' vi-beginning-of-line

# macOS terminal keys, where different
bindkey '^[^[[D' vi-backward-word
bindkey '^[^[[C' vi-forward-word
bindkey '^[[H' beginning-of-line  # FIXME: same on X?
bindkey '^[[F' end-of-line  # FIXME: same on X?

# Plugins

ZSH_PLUGINS="$USER_HOME/.zsh-plugins"

export ZSH="$ZSH_PLUGINS/oh-my-zsh"
ZSH_THEME=jreese
CASE_SENSITIVE="true"
zstyle ':omz:update' mode disabled
COMPLETION_WAITING_DOTS="true"
ZSH_DISABLE_COMPFIX="true"
plugins=(
    docker
    git
    gradle
    npm
    yarn  # requires Perl?!
)
source $ZSH/oh-my-zsh.sh

source "$ZSH_PLUGINS/zsh-z/zsh-z.plugin.zsh"
source "$ZSH_PLUGINS/zsh-command-time/command-time.plugin.zsh"
source "$ZSH_PLUGINS/zsh-autosuggestions/zsh-autosuggestions.zsh"
source "$ZSH_PLUGINS/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"

bindkey '^V' edit-command-line

export LESS='-RFX'
# Colored man pages
# https://wiki.archlinux.org/title/Color_output_in_console#man
# https://man.archlinux.org/man/less.1#D
# https://felipec.wordpress.com/2021/06/05/adventures-with-man-color/
export MANPAGER="less -R --use-color --color=d+c --color=u+b"
export MANROFFOPT="-c"

command_exists() {
    type "$1" >/dev/null 2>&1
}

if command_exists eza
then
    alias ls=eza
fi

if ! command_exists open
then
    alias open=xdg-open
fi

if command_exists bat
then
    alias cat=bat
fi

if command_exists unrar-free
then
    alias unrar=unrar-free
fi

if command_exists nvim
then
    export EDITOR=nvim
    alias vim=nvim
else
    export EDITOR=vim
fi

alias reset='tput reset'

# Theme
autoload -U colors
colors

if [ $UID = 0 ]
then
    PROMPT_HOST_COLOR=$fg[red]
else
    PROMPT_HOST_COLOR=$fg[green]
fi

fancy_terminal () {
    [ -z "$SSH_CLIENT" ] && [ -z "$SSH_TTY" ] && [[ ${TERM:-} = xterm-kitty ]]
}

if [ $HOST = taohua ]
then
    if [ -z "$SSH_CLIENT" ] && [ -z "$SSH_TTY" ]
    then
        PROMPT_HOST=
    else
        PROMPT_HOST=æ¡ƒèŠ±
    fi
elif [ $HOST = liushu ]
then
    PROMPT_HOST=ðŸ’½æŸ³æ ‘
elif [[ $HOST =~ DQ94 ]]
then
    if fancy_terminal
    then
        PROMPT_HOST=$'\UEF32'
    else
        PROMPT_HOST=â–²
    fi
    if [ $UID != 0 ]
    then
        # https://atlassian.design/foundations/color
        PROMPT_HOST_COLOR=$'\e[38;2;0;82;204m'
    fi
else
    PROMPT_HOST=$HOST
fi

PROMPT_BACKGROUND=
PROMPT_ARROW=' '
if fancy_terminal
then
    mk_prompt_color () {
        local COLOR
        if ( grep -q dark $HOME/.config/theme 2>/dev/null )
        then
            COLOR='82;34;119'
        else
            COLOR='255;223;191'
        fi
        PROMPT_BACKGROUND="%{"$'\e'"[48;2;"${COLOR}"m%}"
        PROMPT_ARROW="%{$bg[default]"$'\e'"[38;2;${COLOR}m%}"$'\UE0B0'
    }
    precmd_functions+=(mk_prompt_color)
fi

setopt PROMPT_SUBST
PROMPT='\
$PROMPT_BACKGROUND%{$PROMPT_HOST_COLOR%}$PROMPT_HOST%{$fg[default]%} \
$PROMPT_PWD \
$(git_prompt_info)%{$fg[default]%}\
%{$fg[red]%}%(!.#.Â»)%{$fg[default]%}\
$PROMPT_ARROW%{$reset_color%}'

PROMPT2='$PROMPT_BACKGROUND%{$fg[red]%}\%{$reset_color%} '

if [ -z "$SSH_CLIENT" ] && [ -z "$SSH_TTY" ] && [ $UID != 0 ]
then
    TERM_TITLE_IDLE=""
else
    TERM_TITLE_IDLE="$PROMPT_HOST"
    if [ $UID = 0 ]
    then
        TERM_TITLE_IDLE="${TERM_TITLE_IDLE}# "
    else
        TERM_TITLE_IDLE="${TERM_TITLE_IDLE}: "
    fi
fi

DIR_DECORATIONS=("$HOME/projects" 'â™¦' "$HOME/desktop" 'ðŸ—”' "$HOME/pictures" 'ðŸ–¼' "$HOME/books" 'ðŸ•®' "$HOME" '~')

desktop_func_join() {
    if [ -z "$1" ]
    then
        echo "$2"
    elif [ -z "$2" ]
    then
        echo "$1"
    else
        echo "$1${3:- }$2"
    fi
}

ellipsis='â€¦'
desktop_func_ellipsis() {
    maxlength=$(($2 - ${#ellipsis}))
    if [ "${#1}" -gt "$maxlength" ]
    then
        echo "${1[0,$maxlength]}$ellipsis"
    else
        echo "$1"
    fi
}

pwd_precmd() {
    PWD_SYMBOL=''
    PWD_DIR=$PWD
    for (( i = 1; i <= $#DIR_DECORATIONS; i += 2 )) do
        THIS_DIR=$DIR_DECORATIONS[i]
        THIS_SYM=$DIR_DECORATIONS[$((i + 1))]
        case $PWD in
            ($THIS_DIR)
                PWD_SYMBOL=$THIS_SYM
                PWD_DIR=''
                break
                ;;
            ($THIS_DIR/*)
                PWD_SYMBOL=$THIS_SYM
                PWD_DIR="${PWD#$THIS_DIR/}"
                break
                ;;
        esac
    done
    PROMPT_PWD=$(desktop_func_join "$PWD_SYMBOL" "$PWD_DIR")
    TITLE_DIR=$(desktop_func_ellipsis "$PWD_DIR" 25)
    TITLE_PWD=$(desktop_func_join "$PWD_SYMBOL" "$TITLE_DIR")
    title "${TERM_TITLE_IDLE}${TITLE_PWD}"
}
precmd_functions+=(pwd_precmd)

ZSH_RECORD_EXIT_STATUS=
exit_status_preexec() {
  ZSH_RECORD_EXIT_STATUS=1
}
exit_status_precmd() {
  _EXIT_STATUS_PRECMD="$?"
  if [ -n "${ZSH_RECORD_EXIT_STATUS:-}" ]
  then
    ZSH_RECORD_EXIT_STATUS=
    if [ "$_EXIT_STATUS_PRECMD" -gt 0 ]; then
      echo "$fg[red]âŒ ${_EXIT_STATUS_PRECMD}$fg[default]"
    fi
  fi
}
preexec_functions+=(exit_status_preexec)
precmd_functions+=(exit_status_precmd)

zsh_command_time() {
    if [ -n "$ZSH_COMMAND_TIME" ]; then
        min=$(($ZSH_COMMAND_TIME/60))
        sec=$(($ZSH_COMMAND_TIME%60))
        if [ "$min" -lt 60 ]; then
            timer_show=$(printf '%02d:%02d\n' $min $sec)
        else
            hours=$(($min/60))
            min=$(($min%60))
            timer_show=$(printf '%d:%02d:%02d\n' $hours $min $sec)
        fi
        echo "$fg[cyan]âŒ› $timer_show$fg[default]"
    fi
}
ZSH_COMMAND_TIME_EXCLUDE=(vim nvim)

ZSH_THEME_GIT_PROMPT_PREFIX="%{$fg[green]%}Â±%{$fg[yellow]%}"
ZSH_THEME_GIT_PROMPT_SUFFIX=" "
ZSH_CUSTOM_GIT_PROMPT_STAGED=%{$fg[green]%}âœ¦
ZSH_CUSTOM_GIT_PROMPT_UNSTAGED=%{$fg[red]%}âœ§
ZSH_CUSTOM_GIT_PROMPT_UNTRACKED=%{$fg[yellow]%}âœ§

function parse_git_dirty() {
  local STATUS
  local STAGED
  local UNSTAGED
  local UNTRACKED
  STAGED=
  UNSTAGED=
  UNTRACKED=
  # https://git-scm.com/docs/git-status#_porcelain_format_version_2
  # https://unix.stackexchange.com/a/29748/11020
  for STATUS ("${(@f)$(__git_prompt_git status --porcelain=v2 2>/dev/null)}")
  do
    if [[ "$STATUS" =~ '^\?' ]]; then
      UNTRACKED=1
    else
      if [[ "$STATUS" =~ '^. [^.].' ]]; then
        STAGED=1
      fi
      if [[ "$STATUS" =~ '^. .[^.]' ]]; then
        UNSTAGED=1
      fi
    fi
  done
  if [ -n "$STAGED$UNSTAGED$UNTRACKED" ]
  then
    echo "${STAGED:+$ZSH_CUSTOM_GIT_PROMPT_STAGED}${UNSTAGED:+$ZSH_CUSTOM_GIT_PROMPT_UNSTAGED}${UNTRACKED:+$ZSH_CUSTOM_GIT_PROMPT_UNTRACKED}"
  fi
}

if command_exists dircolors
then
    DIRCOLORS=dircolors
elif command_exists gdircolors
then
    DIRCOLORS=gdircolors
else
    DIRCOLORS=true
fi
export LS_COLORS=$($DIRCOLORS -b $HOME/.dircolors | head -n 1 | sed "s/LS_COLORS='//;s/';//")

if command_exists atuin
then
    eval "$(atuin init zsh --disable-up-arrow)"
fi

ZSHRC_LOCAL=$HOME/.zshrc.local
if [ -f $ZSHRC_LOCAL ]
then
    source $ZSHRC_LOCAL
fi
